apply plugin: 'kotlin'

buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://repo.onarandombox.com/content/groups/public/"
        }
        maven {
            url "https://hub.spigotmc.org/nexus/content/groups/public/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.4.21"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }

    ext.env = System.getenv()
    ext.buildNumber = env.BUILD_NUMBER?.toInteger() ?: "UNKNOWN"
}

configurations {
    all*.exclude group: 'org.jetbrains', module: 'ideaIC'
}

allprojects  {
    apply plugin: 'maven'
    apply plugin: 'org.jetbrains.intellij'
    apply plugin: 'org.jetbrains.dokka'

    group = 'com.dumptruckman.minecraft.pluginbase'
    version = '1.6.0-SNAPSHOT'

    repositories { jcenter() }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://repo.onarandombox.com/content/repositories/thirdparty/")
                snapshotRepository(url: "https://repo.onarandombox.com/content/repositories/thirdparty-dev/")
            }
        }
    }

    configurations {
        all*.exclude group: 'com.jetbrains', module: 'ideaIC'
    }

    patchPluginXml.enabled = false
    prepareTestingSandbox.enabled = false
    runIde.enabled = false
    publishPlugin.enabled = false

    intellij {
        downloadSources = false
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    dokka {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from dokka
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

subprojects {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()

        maven { url "https://repo.onarandombox.com/content/groups/public/" }
        maven { url "https://repo.spongepowered.org/maven" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    ext {
        configurateVersion = "3.6.1"
        kotlinShadedPattern = "pluginbase.kotlin"
        kotlin_version = '1.3.72'
    }
  
    dependencies {
        compileOnly group: 'org.jetbrains', name: 'annotations', version: '19.0.0'
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testCompile group: 'junit', name: 'junit', version: '4.13'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.4'
        testCompile group: 'org.powermock', name: 'powermock-api-easymock', version: '1.7.4'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.7.4'
        testCompile group: 'org.easymock', name: 'easymock', version: '4.2'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }
}
